#!@CSH@

# First argument is $LAPS_DATA_ROOT

# Second argument is number of forecast time steps

# Third argument is the type of statistic

# Fourth argument is Y range upper limit

# Fifth argument is Reflectivity in dbz

# Sixth argument is domain name

# Seventh argument is subdir

setenv LAPSINSTALLROOT @prefix@                                                     
setenv LAPS_DATA_ROOT $1
setenv NLINE $2
setenv STAT $3
setenv YRANGE $4
setenv REFLECTIVITY $5
setenv DOMAIN_NAME_IN $6
setenv SUBDIR $7

setenv DOMAIN_NAME `echo $DOMAIN_NAME_IN | sed 's/_/ /g'`

#setenv LAPS_DATA_ROOT /data/fab/projects/dwr
setenv LOGDIR $LAPS_DATA_ROOT/log

setenv MODELTIME      `head -1 $LAPS_DATA_ROOT/time/modelvtime.dat`
setenv MODELTIME_HHMM `head -1 $LAPS_DATA_ROOT/time/modelvtime.dat | cut -c6-9`

setenv MODEL_ASCIITIME `tail -1 $LAPS_DATA_ROOT/time/modelvtime.dat`

#setenv model wrf-wsm6
#setenv fcsttime 01

setenv STAT_LC `echo $STAT | gawk '{print tolower($0)}'`

setenv FILE $LAPS_DATA_ROOT/lapsprd/verif/$SUBDIR/plot/$REFLECTIVITY/$MODELTIME.$STAT_LC             
setenv MEMBERS_FILE $LAPS_DATA_ROOT/lapsprd/verif/members.txt                                   

setenv VERIF_INTVL `@PERL@ @prefix@/etc/read_nl.pl -d $LAPS_DATA_ROOT -n nest7grid.parms -v verif_intvl`
@ VERIF_FCST_LEN = ( $NLINE - 1 ) * $VERIF_INTVL
@ VERIF_FCST_LEN_HH = $VERIF_FCST_LEN / 3600
#setenv VERIF_FCST_LEN `@PERL@ @prefix@/etc/read_nl.pl -d $LAPS_DATA_ROOT -n nest7grid.parms -v model_fcst_len`
#setenv MODEL_FCST_LEN  `@PERL@ @prefix@/etc/read_nl.pl -d $LAPS_DATA_ROOT -n nest7grid.parms -v model_fcst_len`

# Setup True Type fonts and related environment variables
setenv GNUPLOT_DEFAULT_GDFONT VeraBd                

if (-d /usr/X11R6/lib/X11/fonts/truetype) then
    setenv GDFONTPATH /usr/X11R6/lib/X11/fonts/truetype
else if (-d /usr/share/fonts/bitstream-vera) then
    setenv GDFONTPATH /usr/share/fonts/bitstream-vera
else if (-d /usr/share/fonts/dejavu) then
    setenv GDFONTPATH /usr/share/fonts/dejavu           
    setenv GNUPLOT_DEFAULT_GDFONT DejaVuSans-Bold            
else if (-d /usr/share/fonts/msttcore) then
    setenv GDFONTPATH /usr/share/fonts/msttcore
    setenv GNUPLOT_DEFAULT_GDFONT verdanab            
else
    setenv GDFONTPATH /dev/null
endif

setenv FONTFILE $GDFONTPATH/$GNUPLOT_DEFAULT_GDFONT.ttf

if (-e $FONTFILE) then
    echo "GNUPLOT FONT is $FONTFILE"                                    
#   setenv TERM_FONT 'enhanced font "'$GNUPLOT_DEFAULT_GDFONT', 12"'
    setenv TERM_FONT 'enhanced'
    setenv DEFAULT_FONT 'font "'$GNUPLOT_DEFAULT_GDFONT',12"'
    setenv TITLE_FONT 'font "'$GNUPLOT_DEFAULT_GDFONT',20"'
    setenv MEMBER_FONT 'font "'$GNUPLOT_DEFAULT_GDFONT',16"'
    setenv KEY_HEIGHT 0.3
    setenv KEY_SPACING 0.7
    setenv TH "{/$GNUPLOT_DEFAULT_GDFONT=8 "
    setenv TF \}
else
    echo "FONTFILE $FONTFILE does not exist"
    setenv TERM_FONT ""
    setenv DEFAULT_FONT ""
    setenv TITLE_FONT ""
    setenv MEMBER_FONT ""
    setenv KEY_FONT ""
    setenv KEY_HEIGHT 0
    setenv KEY_SPACING 1.0
    setenv TH ""
    setenv TF ""
endif

if (     $REFLECTIVITY == "20_comp"  || $REFLECTIVITY == "30_comp"  || $REFLECTIVITY == "40_comp" ) then
    setenv SUMMARY_FILE $LAPS_DATA_ROOT/lapsprd/verif/$SUBDIR/plot/summary_comp.txt
    setenv REF `echo $REFLECTIVITY | cut -c1-2`
    setenv DURATION "7-day "
else if ($REFLECTIVITY == "0001_comp"  || $REFLECTIVITY == "0005_comp"  || $REFLECTIVITY == "0010_comp"  || $REFLECTIVITY == "0050_comp"  || $REFLECTIVITY == "0100_comp"  || $REFLECTIVITY == "0200_comp"  || $REFLECTIVITY == "0500_comp") then
    setenv SUMMARY_FILE $LAPS_DATA_ROOT/lapsprd/verif/$SUBDIR/plot/summary_comp.txt
    setenv REF `echo $REFLECTIVITY | cut -c1-4`
    setenv DURATION "7-day "
else if ($REFLECTIVITY == "20_comp2" || $REFLECTIVITY == "30_comp2" || $REFLECTIVITY == "40_comp2") then
    setenv SUMMARY_FILE $LAPS_DATA_ROOT/lapsprd/verif/$SUBDIR/plot/summary_comp2.txt
    setenv REF `echo $REFLECTIVITY | cut -c1-2`
    setenv DURATION "30-day "
else if ($REFLECTIVITY == "0001_comp2" || $REFLECTIVITY == "0005_comp2" || $REFLECTIVITY == "0010_comp2" || $REFLECTIVITY == "0050_comp2" || $REFLECTIVITY == "0100_comp2" || $REFLECTIVITY == "0200_comp2" || $REFLECTIVITY == "0500_comp2") then
    setenv SUMMARY_FILE $LAPS_DATA_ROOT/lapsprd/verif/$SUBDIR/plot/summary_comp2.txt
    setenv REF `echo $REFLECTIVITY | cut -c1-4`
    setenv DURATION "30-day "
else
    setenv SUMMARY_FILE /dev/null
    setenv L_PLOT_CRITERIA T  
    setenv REF $REFLECTIVITY                             
    setenv DURATION ""
endif

if ($SUMMARY_FILE != /dev/null && -e $SUMMARY_FILE) then # Percentage will be present
    setenv L_PLOT_CRITERIA `tail -2 $SUMMARY_FILE | head -1`
    setenv PCT_SUCCESS     `tail -1 $SUMMARY_FILE`
#   setenv L_PLOT_CRITERIA T  # for testing
    if (-d /w3/lapb) then
        if ($DURATION == "") then 
            setenv KEY_WIDTH -19.5 
        else
            setenv KEY_WIDTH -19.5 
        endif
    else
        setenv KEY_WIDTH -7.1
    endif
else                                                     # Percentage will not be present
    if (-d /w3/lapb) then
        setenv KEY_WIDTH -16.2 
    else
        setenv KEY_WIDTH -4.0
    endif
endif

echo "KEY_WIDTH is $KEY_WIDTH"

echo " "
echo "inputs..."
echo "FILE = $FILE"  
echo "VERIF_FCST_LEN = $VERIF_FCST_LEN"
echo "VERIF_FCST_LEN_HH = $VERIF_FCST_LEN_HH"
ls -l $FILE                                                     
if (! -e $FILE) then
    echo "input file $FILE does not exist - exiting..."
    exit
endif

@ NLINE = $NLINE + 1

# obtain xrange times from the data file
setenv STARTTIME1 `head -2      $FILE | tail -1 | cut -c1-11` ; echo $STARTTIME1
setenv STARTTIME2 `head -2      $FILE | tail -1 | cut -c13-23`; echo $STARTTIME2
setenv STOPTIME1  `head -$NLINE $FILE | tail -1 | cut -c1-11` ; echo $STOPTIME1
setenv STOPTIME2  `head -$NLINE $FILE | tail -1 | cut -c13-23`; echo $STOPTIME2

echo " "
echo "Start time: $STARTTIME1 $STARTTIME2"
echo "Stop  time: $STOPTIME1 $STOPTIME2 NLINE is $NLINE"

# Construct gnuplot .gp file
# Fonts are: tiny small medium large giant                                    
setenv GNUPLOTFILE $LAPS_DATA_ROOT/lapsprd/verif/$SUBDIR/plot/$REFLECTIVITY/radar_fcst_$STAT_LC\_$REFLECTIVITY\_$MODELTIME.gp
setenv PNGDIR      $LAPS_DATA_ROOT/lapsprd/verif/$SUBDIR/plot/
setenv PNGFILE     radar_fcst_$STAT_LC\_$REFLECTIVITY\_$MODELTIME.png
setenv DQUOTE \"

echo 'set terminal png '$TERM_FONT' size 1200,600'                        > $GNUPLOTFILE
if ($DURATION == "") then 
    echo 'set xdata time'                                                    >> $GNUPLOTFILE
    echo 'set timefmt "%d-%b-%Y %H:%M:%S"'                                   >> $GNUPLOTFILE
    if ($VERIF_FCST_LEN >= 86400) then
        echo 'set format x "%d/%H"'                                          >> $GNUPLOTFILE
    else
        echo 'set format x "%H:%M"'                                          >> $GNUPLOTFILE
    endif
endif
echo 'set output '$DQUOTE$PNGDIR$PNGFILE$DQUOTE                          >> $GNUPLOTFILE

# time range must be in same format as data file
if ($DURATION == "") then 
    echo 'set xrange ['$DQUOTE$STARTTIME1' '$STARTTIME2$DQUOTE':'$DQUOTE$STOPTIME1' '$STOPTIME2$DQUOTE']' >> $GNUPLOTFILE
else
    echo 'set xrange [00:'$VERIF_FCST_LEN_HH']'                          >> $GNUPLOTFILE
endif
#echo 'set yrange [0:'$YRANGE']'                                         >> $GNUPLOTFILE
if ($STAT == "Bias") then
    echo 'set yrange [0.1:'$YRANGE']'                                    >> $GNUPLOTFILE
    echo 'set ytics (0.1,0.2,0.5,1,2,5,10)'                              >> $GNUPLOTFILE
    echo 'set logscale y'                                                >> $GNUPLOTFILE
else
    echo 'set yrange [0:'$YRANGE']'                                      >> $GNUPLOTFILE
endif
echo 'set grid'                                                          >> $GNUPLOTFILE
echo 'set datafile missing "-999.000"'                                   >> $GNUPLOTFILE

if ($DURATION == "") then
    echo 'set xlabel "Initialized '$MODEL_ASCIITIME' UTC"' $MEMBER_FONT textcolor lt 2      >> $GNUPLOTFILE
else
#   echo 'set xlabel "Ending on '$MODEL_ASCIITIME' UTC"' $MEMBER_FONT textcolor lt 2      >> $GNUPLOTFILE
    echo 'set xlabel "Forecast Hours - Latest Run At '$MODEL_ASCIITIME' UTC"' $MEMBER_FONT textcolor lt 2       >> $GNUPLOTFILE 
endif

if ($SUBDIR == "REF") then
    echo 'set ylabel "Volume Reflectivity '$STAT'"'  $MEMBER_FONT textcolor lt 2      >> $GNUPLOTFILE
    echo 'set title "Radar 3-D Reflectivity '$REF'dBZ '$DURATION$STAT' ('$DOMAIN_NAME' domain)" '$TITLE_FONT >> $GNUPLOTFILE
else if ($SUBDIR == "LMR") then
    echo 'set ylabel "Composite Reflectivity '$STAT'"'  $MEMBER_FONT textcolor lt 2      >> $GNUPLOTFILE
    echo 'set title "Composite Reflectivity '$REF'dBZ '$DURATION$STAT' ('$DOMAIN_NAME' domain)" '$TITLE_FONT >> $GNUPLOTFILE
else # Stage IV Precip
    setenv PCP_HR    `echo $SUBDIR | cut -c5-6 | sed 's/^0//'`
    setenv REF_LABEL `echo $REF | cut -c1-2 | sed 's/^0//'`.`echo $REF | cut -c3-4`
    echo "REF: $REF"
    echo "REF_LABEL: $REF_LABEL"
    echo 'set ylabel "'$PCP_HR' Hour Stage IV Precip '$REF_LABEL'in '$STAT'"'  $MEMBER_FONT textcolor lt 2      >> $GNUPLOTFILE
    echo 'set title  "'$PCP_HR' Hour Stage IV Precip '$REF_LABEL'in '$DURATION$STAT' ('$DOMAIN_NAME' domain)" '$TITLE_FONT >> $GNUPLOTFILE
endif

echo 'set key ins'                                                       >> $GNUPLOTFILE
echo 'set key right box'                                                 >> $GNUPLOTFILE
echo 'set key spacing' $KEY_SPACING                                      >> $GNUPLOTFILE
echo 'set key height' $KEY_HEIGHT                                        >> $GNUPLOTFILE
echo 'set key width' $KEY_WIDTH                                          >> $GNUPLOTFILE

setenv NMEMBERS `wc -l $MEMBERS_FILE | cut -c1-2`
echo "NMEMBERS: $NMEMBERS"

setenv IMEMBER 1
while ($IMEMBER <= $NMEMBERS)  
#   setenv ICOL $IMEMBER
    @ ICOL = $IMEMBER + 2
    
    setenv MEMBER `head -$IMEMBER $MEMBERS_FILE | tail -1`

#   if ($MEMBER == persistence) then # shorten appearance in the title
#       setenv MEMBER persist 
#   endif

    if ($SUMMARY_FILE != /dev/null && -e $SUMMARY_FILE) then # Percentage will be present
        setenv PCT `head -$IMEMBER $SUMMARY_FILE | tail -1`
        setenv TITLE "`echo $MEMBER | sed 's/_/ /g'` $PCT_SUCCESS/$PCT%"

        echo "PCT_SUCCESS being compared to threshold of 80 is: $PCT_SUCCESS"
        if ($PCT_SUCCESS >= 80) then
            setenv LS 1
            if (-d /w3/lapb) then
                setenv LW 3
            else
                setenv LW 2
            endif
        else 
            setenv LS 2
            setenv LW 1
        endif

    else                                                     # Percentage not present
        setenv PCT ""
        setenv TITLE `echo $MEMBER | sed 's/_/ /g'`
        setenv LS 1
        if (-d /w3/lapb) then
            setenv LW 3
        else
            setenv LW 2
        endif

    endif

    echo "IMEMBER MEMBER ICOL TITLE: $IMEMBER $MEMBER $ICOL $TITLE"

    if ($DURATION == "") then 
        setenv TIME 1
    else
        setenv TIME \(\$1+\$2/60\)
    endif

    if ($NMEMBERS == 1) then
        echo 'plot '$DQUOTE$FILE$DQUOTE' index 0 using '$TIME':'$ICOL'  title "'"$TH"$TITLE$TF'" with linespoints lw '$LW' lt '$IMEMBER' pt 5 ps 1'       >> $GNUPLOTFILE
    else if ($IMEMBER == 1) then
        echo 'plot '$DQUOTE$FILE$DQUOTE' index 0 using '$TIME':'$ICOL'  title "'"$TH"$TITLE$TF'" with linespoints lw '$LW' lt '$IMEMBER' pt 5 ps 1, \'    >> $GNUPLOTFILE
    else if ($IMEMBER == $NMEMBERS) then
        echo '     '$DQUOTE$FILE$DQUOTE' index 0 using '$TIME':'$ICOL'  title "'"$TH"$TITLE$TF'" with linespoints lw '$LW' lt '$IMEMBER' pt 5 ps 1'       >> $GNUPLOTFILE
    else
        echo '     '$DQUOTE$FILE$DQUOTE' index 0 using '$TIME':'$ICOL'  title "'"$TH"$TITLE$TF'" with linespoints lw '$LW' lt '$IMEMBER' pt 5 ps 1, \'    >> $GNUPLOTFILE
    endif

    @ IMEMBER = $IMEMBER + 1

end

# Run gnuplot
gnuplot < $GNUPLOTFILE

echo " "
echo "outputs..."
ls -l $GNUPLOTFILE
ls -l $PNGDIR$PNGFILE
ls -l $PNGDIR/radar_fcst_$STAT_LC\_$REFLECTIVITY.png

if ($L_PLOT_CRITERIA == "F") then
    echo "exiting to skip links, thumbnail, and animation sections - plot criteria flag is false"
    exit
endif

cd $PNGDIR
rm -f          radar_fcst_$STAT_LC\_$REFLECTIVITY.png
ln -s $PNGFILE radar_fcst_$STAT_LC\_$REFLECTIVITY.png

rm -f          radar_fcst_$STAT_LC\_$REFLECTIVITY\_$MODELTIME_HHMM.png
ln -s $PNGFILE radar_fcst_$STAT_LC\_$REFLECTIVITY\_$MODELTIME_HHMM.png

# Make Thumbnail
rm -f                            radar_fcst_$STAT_LC\_$REFLECTIVITY\_$MODELTIME_HHMM\_thumb.png
convert -resize 105x70! $PNGFILE radar_fcst_$STAT_LC\_$REFLECTIVITY\_$MODELTIME_HHMM\_thumb.png

# Make Animation
convert -delay 150 -loop 0 `ls -rt radar_fcst_$STAT_LC\_$REFLECTIVITY\_???[0-9].png` $PNGFILE anim24hr_radar_fcst_$STAT_LC\_$REFLECTIVITY.gif
